# model description:
#
# There exist a train and a crossing barrier on a
# circular track. The train is moving around the track.
# The barrier closes or opens depending on the distance
# to the train. The goal of the barrier is to prevent others
# from entering the track when the train passes by.


bool a;
bool b;

[-20, 100] tx;
[0, 90] bx;
[-50, 50] vx;
{
    mode:
        a = false;
        b = false;
    inv:
	tx > 60;
    flow:
        tx(t) = -5 * t  + tx(0);
        bx(t) = -0.01 * t * t + 0.03 *t + bx(0);
        vx(t) = 0.01 * t + vx(0) ;
    jump:
        (and (70 <= tx) (tx <= 75)) =>
          (and (not a') b' (bx' = bx) (tx' = tx) (vx' = vx));
        (and (60 <= tx) (tx < 70)) =>
          (and (a') (not b') (bx' = bx) (tx' = tx) (vx' = vx)) ;
}
{
    mode:
        a = false;
        b = true;
    inv:
	tx > 40;
    flow:
        tx(t) = -5 * t  + tx(0);
        bx(t) = -0.01 * t * t + 0.03 *t + bx(0);
        vx(t) = 0.02 * t + vx(0) ;
    jump:
        (tx < 50) =>
          (and (a') (not b') (bx' = bx) (tx' = tx) (vx' = vx)) ;
        (tx < 10) =>
          (and (a') (b') (bx' = bx) (tx' = tx) (vx' = vx)) ;
}
{
    mode:
        a = true;
        b = false;
    inv:
	tx < 10;
    flow:
        tx(t) = -5 * t  + tx(0);
        bx(t) = 0.01 * t * t + 0.01  * t + bx(0);
        vx(t) = 0.01 * t + vx(0) ;
    jump:
       (and (-5 <= tx) (tx < 5) (bx <= 80)) => 
          (and a' b' (bx' = bx) (tx' = tx) (vx' = vx));
        (and (bx > 85) (tx < -8)) =>
          (and (not a') (not b') (bx' = bx) (tx' = (100 + tx)) (vx' = vx));
}
{
    mode:
        a = true;
        b = true;
    inv:
	tx > -10;
    flow:
        tx(t) = -5 * t  + tx(0);
        bx(t) = -0.02 * t * t + bx(0) + 0.001;
        vx(t) = -0.01 * t + vx(0) ;
    jump:
        (bx > 85) =>
          (and (not a') (not b') (bx' = bx) (tx' = (100 + tx)) (vx' = vx));
}
init:
(and (a = false) (b = false) (bx >= 0) (bx < 1) (tx >= 85) (tx <= 90) (vx <= 0.1) (vx >= 0)) ;

propositions:
txg60 = tx > 60;
txg10 = tx > 10;
bxg80 = bx > 80;

goal:
([] [0.0,50.0) (tx > 0)); # true
(<> [0, 20] (tx < -5)); # false
([] [0, 40] (bxg80 -> (<> [0, 20) txg10))); # true
(bx < 10) U [10, 40] (<> [0, 20] (tx < 40)); # false
