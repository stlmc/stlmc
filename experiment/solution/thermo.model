# model description:
#
# Two rooms are connected by an open door.
# The room' temperature is controlled by
# its own heater and changes over time.
# It is also affected by each other's room
# temperature.

bool a;
[10, 30] x1;
bool b;
[10, 30] x2;
{
    mode:
        a = false;
        b = false ;
    inv:
        x1 >= 18;
        (x2 >= 18);
    flow:
        x1(t) = -0.3 * (t + 0.002 * t * t) + x1(0);
        x2(t) = -0.5 * (t + 0.002 * t * t) + x2(0);
    jump:
         x2 < 22 =>
          (and (a' = false) (b' = true) (x1' = x1) (x2' = x2));
         x1 < 22 =>
          (and (a' = true) (b' = false) (x1' = x1) (x2' = x2));
}
{
    mode:
        a = false;
        b = true;
    inv:
        x1 >= 18;
        (x2 <= 24);
    flow:
        x1(t) = -0.3 * (t + 0.002 * t * t) + x1(0);
        x2(t) = 2 * (0.5 * t + 0.0025 * t* t) + x2(0);
    jump:
        x1 < 22 =>
          (and (a' = true) (b' = true) (x1' = x1) (x2' = x2));
        x2 > 20 =>
          (and (a' = false) (b' = false) (x1' = x1) (x2' = x2));
}
{
    mode:
        a = true;
        b = false;
    inv:
        x1 <= 24;
        (x2 >= 18);
    flow:
        x1(t) =  1.5 * (0.5 * t + 0.0025 * t* t) + x1(0);
        x2(t) = -0.5 * (t + 0.002 * t * t) + x2(0);
    jump:
        x2 < 22 =>
          (and (a' = true) (b' = true) (x1' = x1) (x2' = x2));
        x1 > 20 =>
          (and (a' = false) (b' = false) (x1' = x1) (x2' = x2));

}
{
    mode:
        a = true;
        b = true;
    inv:
        x1 <= 24;
        (x2 <= 24);
    flow:
        x1(t) =  1.5 * (0.5 * t + 0.0025 * t* t) + x1(0);
        x2(t) = 2 * (0.5 * t + 0.0025 * t* t) + x2(0);
    jump:
         x1 > 20 =>
          (and (a' = false) (b' = true) (x1' = x1) (x2' = x2));
         x2 > 20  =>
          (and (a' = true) (b' = false) (x1' = x1) (x2' = x2));

}


init:
(and not(a) not(b) (x1 = 20) (x2 = 23));

propositions:
reachability = x1 >= 21;
p = x2 > 22 ;
q = x1 < 22;
r = x1 < 20;
s = x2 > 19;

#time bound 20
goal:
([][5, 15] x1 <= 20); # true
([](2, 5) (~a R [0, 10] x2 > 20)); # true
(a R [0, 10] x1 < 21);
(<> [0, 4] (a and [] [2, 5] x1 < 19));


