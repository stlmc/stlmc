# model description:
#
# Two rooms are connected by an open door.
# The room' temperature is controlled by
# its own heater and changes over time.
# It is also affected by each other's room
# temperature.

bool a;
[0, 50] x1;
[0, 50] x2;
{
    mode:
        a = false;
    inv:
        x1 > 10;
        (x2 > 10);
    flow:
        d/dt[x1] = -0.015 * x1 + 0.01 * x2;
        d/dt[x2] = -0.045 * x2 + 0.01 * x1;
    jump:
        x1 < 16 => 
          (and (a' = true) (x1' = x1) (x2' = x2));
	(and (16 < x1) (x1 < 24)) =>
	  (and (a' = false) (x1' = x1) (x2' = x2));

}
{
    mode:
        a = true;
    inv:
        x1 < 30;
        (x2 < 30);
    flow:
        d/dt[x1] = 0.015 * x1 + 0.01 * x2;
        d/dt[x2] = 0.045 * x2 + 0.01 * x1;
    jump:
        x1 >= 24 =>
          (and (a' = false) (x1' = x1) (x2' = x2));
        (and (16 < x1) (x1 < 24)) =>
          (and (a' = true) (x1' = x1) (x2' = x2));
}
init:
(and (a = false) (x1 <= 20 + 0.1) (x1 >= 20 - 0.1) (x2 <= 20 + 0.1) (x2 >= 20 - 0.1));

propositions:
reachability = x1 >= 21;
p = x2 > 19 ;
q = x1 <= 21;
x1T = x1 > 27 ;
x2T = x2 > 27 ;

goal:
reach reachability;
#<> [0.0, 50.0] x1T;

