# model description:
#
# Two water tanks are connected by a single pipe.
# The water level of each tank is separately controlled
# by each pump and is affected by the adjacent water level.
# time bound : 1

bool a;
[0, 10] x1;
{
    mode:
        a = false;
    inv:
        x1 > 0;
    flow:
        d/dt[x1] = (-0.5 * sqrt(2 * 9.806) * sqrt(x1)) / 8 ;
    jump:
        (x1 >= 5)  =>
          (and (not a') (x1' = x1));
        (x1 < 5) =>
          (and a' (x1' = x1));

}
{
    mode:
        a = true;
    inv:
        x1 < 8;
    flow:
        d/dt[x1] = (5 - 0.5 * sqrt(2 * 9.806) * sqrt(x1)) / 8 ;
    jump:
        (x1 >= 5)  =>
          (and (not a') (x1' = x1));
        (x1 < 5) =>
          (and a' (x1' = x1));
}
init:
(and (a = true) (x1 <= 4 + 0.1) (x1 >= 4 - 0.1));

propositions:
reachability = x1 >= 5.6 ;


goal:
(<> [0.0, 50.0) reachability) ; # false
((x1 > 3) R (2, 12] (~ a)); # true 
([] [0.0, 30.0] ((x1 < 3) -> <> [0.0, 10.0] a)) ; # true
(<> [4, 12] ((~ a) R [1, 12] x1 > 4)); # false

