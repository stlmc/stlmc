bool a;
[0, 10] x1;
bool b;
[0, 10] x2;
{
    mode:
        a = false;
        b = false ;
    inv:
        x1 > 1;
        (x2 > 1);
    flow:
        d/dt[x1] = -0.8;
        d/dt[x2] = -0.6;
    jump:
        x2 < 6 =>
          (and (not a') b' (x1' = x1) (x2' = x2));
        (x1 < 5) =>
          (and a' (not b') (x1' = x1) (x2' = x2));
}
{
    mode:
        a = false;
        b = true;
    inv:
        x1 > 1;
        (x2 < 9);
    flow:
        d/dt[x1] = -0.8;
        d/dt[x2] = 1;
    jump:
        x2 > 2 =>
          (and (not a') (not b') (x1' = x1) (x2' = x2));
        x1 < 5 =>
          (and a' b' (x1' = x1) (x2' = x2));
}
{
    mode:
        a = true;
        b = false;
    inv:
        x1 < 8;
        (x2 > 1);
    flow:
        d/dt[x1] = 0.9;
        d/dt[x2] = -0.6;
    jump:
        x1 > 2 =>
          (and (not a') (not b') (x1' = x1) (x2' = x2));
        x2 < 6 =>
          (and a' b' (x1' = x1) (x2' = x2));
}
{
    mode:
        a = true;
        b = true;
    inv:
        x1 < 9;
        (x2 < 9);
    flow:
        d/dt[x1] = 0.9;
        d/dt[x2] = 1;
    jump:
        x1 > 2 =>
          (and (not a') b' (x1' = x1) (x2' = x2));
        x2 < 2 =>
          (and a' (not b') (x1' = x1) (x2' = x2));
}
init:
(and (not(a)) not(b) (4.4 <= x1) (x1 <= 4.5)  (5.9 <= x2) (x2 <= 6)); 

propositions:
x1g2 = x1 > 2;
x1l4 = x1 < 4;
x2l4 = x2 < 4;
x2g6 = x2 > 6;
pa = a;
pb = b;

# timebound : 40
goal:
<>[3, 8] x1 <= 7; # true
<>[1, 3](x2 > 2 R[1, 2] pb); # true
<>[5, 9] x2 < 5; # false at 13
(<> [1, 3) x2 > 5.8) R[4, 7] ~pb; # false at 17
