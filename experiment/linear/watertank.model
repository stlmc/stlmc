# model description:
#
# Two water tanks are connected by a single pipe.
# The water level of each tank is separately controlled
# by each pump and is affected by the adjacent water level.

bool a;
[0, 10] x1;
bool b;
[0, 10] x2;
{
    mode:
        a = false;
        b = false ;
    inv:
        x1 > 1;
        (x2 > 1);
    flow:
        d/dt[x1] = -0.2;
        d/dt[x2] = -0.3;
    jump:
        x2 < 6 =>
          (and (not a') b' (x1' = x1) (x2' = x2));
        (x1 < 5) =>
          (and a' (not b') (x1' = x1) (x2' = x2));
}
{
    mode:
        a = false;
        b = true;
    inv:
        x1 > 1;
        (x2 < 9);
    flow:
        d/dt[x1] = -0.2;
        d/dt[x2] = 0.6;
    jump:
        x2 > 2 =>
          (and (not a') (not b') (x1' = x1) (x2' = x2));
        x1 < 5 =>
          (and a' b' (x1' = x1) (x2' = x2));
}
{
    mode:
        a = true;
        b = false;
    inv:
        x1 < 8;
        (x2 > 1);
    flow:
        d/dt[x1] = 0.5;
        d/dt[x2] = -0.3;
    jump:
        x1 > 2 =>
          (and (not a') (not b') (x1' = x1) (x2' = x2));
        x2 < 6 =>
          (and a' b' (x1' = x1) (x2' = x2));
}
{
    mode:
        a = true;
        b = true;
    inv:
        x1 < 9;
        (x2 < 8);
    flow:
        d/dt[x1] = 0.5;
        d/dt[x2] = 0.6;
    jump:
        x1 > 2 =>
          (and (not a') b' (x1' = x1) (x2' = x2));
        x2 < 2 =>
          (and a' (not b') (x1' = x1) (x2' = x2));
}
init:
(and (not(a)) not(b) (x1 = 5)  (x2 = 6));

propositions:
x1g2 = x1 > 2;
x1l4 = x1 < 4;
x2l4 = x2 < 4;
x2g6 = x2 > 6;

goal:
([] [0, 50] x1 <= 6); # false at bound 2
(<> [2, 100] x1 < 5); # false at bound 1
([] [2,8] x2 > 4); # false at bound 3
([] [1, 5] x2 > 4); # true
(<> [0, 40] ((x1 > 3) R [0, 40) a)); # true
([] [5, 30] (<> (0, 15] x2g6)); # false

