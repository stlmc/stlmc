real h;
real start;
const real k = 0.025 ;
(20, 30] x;

{
    mode:
      (and (h = 0) (start >= 27)) ;
    inv:
      (and (x > 23) (x <= 30));
    flow:
      d/dt[x] = -k * ((1 - 2 * 0.01) * start);
    jump:
      (and (x > 24) (x < 27)) => 
         (and (h' >= 40) (h' <= 50) (start' = x) (x' = x)) ;
      (and (x > 23) (x <= 25)) => 
         (and (h' >= 90) (h' < 100) (start' = x) (x' = x)) ;
}

{
    mode:
      (and (h > 0) (start <= 27)) ;
    inv:
      (and (x > 20) (x <= 29));
    flow:
      x(t) = 0.013 * h + k * (1 - 0.0246 * t) + 0.006 * (1 - 0.0442 * t) + x(0);
    jump:
      (x >= 27) => 
        (and (h' = 0) (start' = x) (x' = x)) ;
}

init:
(and (h = 0) (start = x) (x >= 27) (x <= 30) );

propositions:
powerOn = h > 0 ;

goal:
#(<> [0.0, 40.0] (x > 21)) ;
([] [0.0, 20.0] (h <= 0 -> <>[0.0, 5.0] powerOn)) ;






